#
# FIXME Add license header (in fixup commit)
#
# Store variables as files so that they can later be used by other tools
# 
# Functions:
#
#   write_config_info (DIRECTORY <dir> VARIABLES <var>...)
#       For each variable writes a file with the name and the content of the
#       variable to the specified directory.
# 
function(write_config_info)
    list(GET ARGV 0 DIR_SPEC)
    list(GET ARGV 1 CONFIG_INFO_DIR)
    list(GET ARGV 2 VAR_SPEC)
    list(SUBLIST ARGV 3 -1 VARS)

    if(NOT ${DIR_SPEC} STREQUAL "DIRECTORY")
        message(FATAL_ERROR "No directory specified")
        return()
    endif()

    if(NOT ${VAR_SPEC} STREQUAL "VARIABLES")
        message(FATAL_ERROR "No variables specified")
        return()
    endif()
    
    set(COMMENT_FILE "${CONFIG_INFO_DIR}/#The contents of this directory are generated by CMake!")
    if(EXISTS ${COMMENT_FILE})
        # Assume this directory was generated by this function and it's fine to delete it.
        file(REMOVE_RECURSE ${CONFIG_INFO_DIR})
    elseif(EXISTS ${CONFIG_INFO_DIR})
        message(WARNING "Might pollute existing directory with config info")
    endif()

    if(NOT EXISTS ${CONFIG_INFO_DIR})
        # This will also create the necessary parent directories.
        file(WRITE ${COMMENT_FILE} "")
    endif()
            
    message("Writing variables to ${CONFIG_INFO_DIR}:")
    foreach(VAR ${VARS})
        file(WRITE ${CONFIG_INFO_DIR}/${VAR} ${${VAR}})    
        message("    ${VAR} : ${${VAR}}")
    endforeach()
endfunction()
